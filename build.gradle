buildscript {
    repositories {
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://dl.bintray.com/igordmn/maven' }
        maven { url 'https://clojars.org/repo' }
        maven { url 'https://jitpack.io' }
        flatDir { dirs new File(rootProject.projectDir, 'libs') }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta5'
        classpath 'com.github.JakeWharton:sdk-manager-plugin:220bf7a88a7072df3ed16dc8466fb144f2817070'
        classpath 'me.tatarka:gradle-retrolambda:3.2.4'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.7'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        configurations.classpath.exclude group: 'com.android.tools.external.lombok'
    }
}

plugins {
    id "org.sonarqube" version "1.1"
}

ext {
    config = Utils.loadProperties('.sign/sign.properties', 'workspace.properties');
}

def androidProjects = [project(':app'), project(':util:android')]
def javaProjects = [project(':util:java')]

configure(androidProjects + javaProjects) {
    afterEvaluate { project ->
        repositories {
            jcenter()
            maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
            maven { url 'https://dl.bintray.com/igordmn/maven' }
            maven { url 'https://clojars.org/repo' }
            maven { url 'https://jitpack.io' }
            flatDir { dirs new File(rootProject.projectDir, 'libs') }
        }

        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }

        dependencies {
            compile 'com.google.code.findbugs:jsr305:3.0.0'
            compile 'com.google.guava:guava:18.0'
            compile 'net.sf.trove4j:trove4j:3.0.3'
            compile 'net.sourceforge.streamsupport:streamsupport:1.2.1'
            compile 'com.jakewharton:disklrucache:2.0.2'
            testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
            testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
            testCompile 'cglib:cglib-nodep:3.1'
            testCompile 'org.objenesis:objenesis:2.2'
        }
    }
}

configure(androidProjects) {
    afterEvaluate {
        android {
            compileSdkVersion 23
            buildToolsVersion '23.0.2'

            defaultConfig {
                minSdkVersion 16
                targetSdkVersion 23
            }

            lintOptions {
                disable 'InvalidPackage'
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            aaptOptions {
                ignoreAssetsPattern "!.readme"
            }
        }

        dependencies {
            compile ':simplefragment:'
            compile ':simplefragment-appcompat:'
            compile 'com.android.support:appcompat-v7:23.1.1'
            compile 'com.android.support:support-annotations:23.1.1'
            compile 'com.android.support:cardview-v7:23.1.1'
            compile 'com.android.support:recyclerview-v7:23.1.1'
            compile 'com.android.support:palette-v7:23.1.1'
            compile 'com.android.support:design:23.1.1'
            compile 'org.adw.library:discrete-seekbar:1.0.0'
            compile 'com.rengwuxian.materialedittext:library:1.8.2'
            compile 'com.pnikosis:materialish-progress:1.3'
            compile 'com.jpardogo.materialtabstrip:library:1.0.8'
            compile 'com.mikepenz:materialdrawer:3.0.8'
            compile 'com.jakewharton.timber:timber:4.1.0'
            compile 'com.squareup.dagger:dagger:1.2.2'
            compile 'com.jakewharton:butterknife:7.0.1'
            compile 'frankiesardo:icepick:3.1.0'
            apt 'frankiesardo:icepick-processor:3.1.0'
            apt 'com.squareup.dagger:dagger-compiler:1.2.2'
        }
    }
}
