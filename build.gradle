buildscript {
    ext {
        config = Utils.loadProperties(".sign/sign.properties", "workspace.properties")

        projectVersions = [
                project: "0.7",
                kotlin: "1.0.0",
                supportLibrary: "23.1.1",
                buildTools: "23.0.2",
                minSdk: 16,
                targetSdk: 23,
                compileSdk: 23,
        ]

        projectRepositories = {
            jcenter()
            maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
            maven { url "https://dl.bintray.com/igordmn/maven" }
            maven { url "https://clojars.org/repo" }
            maven { url "https://jitpack.io" }
            flatDir { dirs new File(projectDir, "lib") }
        }

        projectDependencies = [
                common: [
                        "org.jetbrains.kotlin:kotlin-stdlib:$projectVersions.kotlin",
                        "com.google.guava:guava:19.0",
                        "com.carrotsearch:hppc:0.7.1",
                        "com.jakewharton:disklrucache:2.0.2",
                ],

                android: [
                        ":simplefragment:",
                        ":simplefragment-appcompat:",
                        ":readiumSdk:@aar",
                        "com.android.support:appcompat-v7:$projectVersions.supportLibrary",
                        "com.android.support:support-annotations:$projectVersions.supportLibrary",
                        "com.android.support:cardview-v7:$projectVersions.supportLibrary",
                        "com.android.support:recyclerview-v7:$projectVersions.supportLibrary",
                        "com.android.support:palette-v7:$projectVersions.supportLibrary",
                        "com.android.support:design:$projectVersions.supportLibrary",
                        "org.adw.library:discrete-seekbar:1.0.0",
                        "com.rengwuxian.materialedittext:library:1.8.2",
                        "com.pnikosis:materialish-progress:1.3",
                        "com.jpardogo.materialtabstrip:library:1.0.8",
                        "com.mikepenz:materialdrawer:3.0.8",
                        "com.jakewharton.timber:timber:4.1.0",
                        "com.squareup.dagger:dagger:1.2.2",
                        "com.jakewharton:butterknife:7.0.1",
                        "frankiesardo:icepick:3.1.0",
                        "org.jetbrains.anko:anko-sdk15:0.8.2",
                        "org.jetbrains.anko:anko-appcompat-v7:0.8.2",
                ],

                testCommon: [
                        "org.spockframework:spock-core:1.0-groovy-2.4",
                        "org.codehaus.groovy:groovy-all:2.4.3",
                        "cglib:cglib-nodep:3.1",
                        "org.objenesis:objenesis:2.2",
                ]
        ]
    }

    repositories projectRepositories

    dependencies {
        classpath "com.android.tools.build:gradle:2.0.0-beta6"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$projectVersions.kotlin"
        classpath "com.github.JakeWharton:sdk-manager-plugin:220bf7a88a7072df3ed16dc8466fb144f2817070"
        classpath "org.codehaus.groovy:gradle-groovy-android-plugin:0.3.7"
    }
}

plugins {
    id "org.sonarqube" version "1.2"
}

subprojects {
    repositories projectRepositories
}
