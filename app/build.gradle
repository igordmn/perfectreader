apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion projectVersions.compileSdk
    buildToolsVersion projectVersions.buildTools

    defaultConfig {
        applicationId 'com.dmi.perfectreader'
        versionName versionNameFromGit(rootProject.projectDir)
        versionCode versionCodeFromGit(rootProject.projectDir)
        setProperty('archivesBaseName', "$rootProject.name-$versionName")

        minSdkVersion projectVersions.minSdk
        targetSdkVersion projectVersions.targetSdk

        vectorDrawables.useSupportLibrary = true
    }

    if (config['sign.enabled']) {
        signingConfigs {
            main {
                storeFile new File(rootProject.projectDir, '.sign/' + config['sign.store.file'])
                storePassword config['sign.store.password']
                keyAlias config['sign.key.alias']
                keyPassword config['sign.key.password']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled true
            signingConfig config['sign.enabled'] ? signingConfigs.main : null
            proguardFiles 'proguard.pro', 'proguard-debug.pro'
            testProguardFiles 'proguard.pro', 'proguard-debug.pro'
            buildConfigField 'boolean', 'DEBUG_LEAKCANARY', config['debug.leakCanary'] ?: 'false'
            buildConfigField 'boolean', 'DEBUG_BLOCKCANARY', config['debug.blockCanary'] ?: 'false'
            buildConfigField 'int', 'DEBUG_BLOCKCANARY_LIMIT_MILLIS', config['debug.blockCanary.limitMillis'] ?: '500'
            buildConfigField 'boolean', 'DEBUG_STRICTMODE', config['debug.strictMode'] ?: 'false'
            buildConfigField 'boolean', 'DEBUG_SHOWRENDERFREEZES', config['debug.showRenderFreezes'] ?: 'false'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig config['sign.enabled'] ? signingConfigs.main : null
            proguardFiles 'proguard.pro', 'proguard-release.pro'
            testProguardFiles 'proguard.pro', 'proguard-release.pro'
            buildConfigField 'boolean', 'DEBUG_LEAKCANARY', 'false'
            buildConfigField 'boolean', 'DEBUG_BLOCKCANARY', 'false'
            buildConfigField 'int', 'DEBUG_BLOCKCANARY_LIMIT_MILLIS', '500'
            buildConfigField 'boolean', 'DEBUG_STRICTMODE', 'false'
            buildConfigField 'boolean', 'DEBUG_SHOWRENDERFREEZES', 'false'
        }
    }

    sourceSets {
        debug.java.srcDirs += 'src/debug/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    aaptOptions {
        ignoreAssetsPattern '!.readme'
    }

    lintOptions {
        disable 'ViewConstructor'
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    compile project(':util:util-common')
    compile project(':util:util-android')

    debugCompile 'com.github.moduth:blockcanary-android:1.2.1'
    releaseCompile 'com.github.moduth:blockcanary-no-op:1.2.1'
    testCompile 'com.github.moduth:blockcanary-no-op:1.2.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-SNAPSHOT'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'

    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude module: 'support-annotations'
    }

    androidTestCompile project(':util:util-testCommon')
    testCompile project(':util:util-testCommon')
    testCompile 'junit:junit:4.12'
}