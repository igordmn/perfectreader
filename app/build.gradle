buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'android-apt'

configurations {
    apt
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId 'com.dmi.perfectreader'
        versionCode rootProject.buildCounter
        versionName rootProject.version.toString()
        minSdkVersion 15
        targetSdkVersion 21
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    if (rootProject.signProperties != null) {
        signingConfigs {
            main {
                storeFile new File(rootProject.projectDir, '.sign/' + rootProject.signProperties['store.file'])
                storePassword rootProject.signProperties['store.password']
                keyAlias rootProject.signProperties['key.alias']
                keyPassword rootProject.signProperties['key.password']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig rootProject.signProperties ? signingConfigs.main : null
        }
        release {
            signingConfig rootProject.signProperties ? signingConfigs.main : null
        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName variant.applicationId
    }
}

dependencies {
    compile project(':core')
    compile project(':readiumSdk')
    compile 'com.android.support:support-annotations:20.0.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'org.androidannotations:androidannotations-api:3.2'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:palette-v7:21.0.3'

    apt 'org.androidannotations:androidannotations:3.2'
}

ext {
    archivesBaseName = rootProject.name + '-' + rootProject.version + '-b' + rootProject.buildCounter
}

sonarRunner {
    sonarProperties {
        properties['sonar.sources'] = 'src/main/java'
        properties['sonar.binaries'] = 'build/intermediates/classes/debug'
        properties['sonar.profile'] = 'Android Lint'
    }
}
