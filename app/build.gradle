import com.android.build.OutputFile
import Utils
import AndroidUtils

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'

def buildCounter = Integer.getInteger('build.counter', 0)
def signProperties = Utils.rootProperties('.sign/sign.properties')

android {
    defaultConfig {
        applicationId 'com.dmi.perfectreader'
        versionName rootProject.version.toString()
    }

    if (signProperties != null) {
        signingConfigs {
            main {
                storeFile new File(rootProject.projectDir, '.sign/' + signProperties['store.file'])
                storePassword signProperties['store.password']
                keyAlias signProperties['key.alias']
                keyPassword signProperties['key.password']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled true
            signingConfig signProperties ? signingConfigs.main : null
            proguardFiles 'proguard-android.pro', 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signProperties ? signingConfigs.main : null
            proguardFiles 'proguard-android.pro', 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'mips', 'x86', 'arm64-v8a', 'mips64', 'x86_64'
        }
    }

    // Each APK must have a different version code
    // (see "Rules for multiple APKs" at http://developer.android.com/intl/ru/google/play/publishing/multiple-apks.html)
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def buildNumber = buildCounter
            def abi = output.getFilter(OutputFile.ABI)
            output.versionCodeOverride = AndroidUtils.computeVersionCode(buildNumber, abi)
        }
    }
}

dependencies {
    compile project(':libs:readiumSdk')
    compile project(':util:android')
    compile project(':typoweb')
}

ext {
    archivesBaseName = rootProject.name + '-' + rootProject.version + '-b' + buildCounter
}
