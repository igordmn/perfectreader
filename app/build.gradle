buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.4'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.dmi.gradle:android-ndk-classic:0.2.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'android-ndk-classic'

configurations {
    apt
}

android {
    compileSdkVersion 19
    buildToolsVersion '20'

    defaultConfig {
        applicationId 'com.dmi.perfectreader'
        versionCode rootProject.buildCounter
        versionName rootProject.version.toString()
        minSdkVersion 15
        //noinspection OldTargetApi
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    if (rootProject.signProperties != null) {
        signingConfigs {
            main {
                storeFile new File(rootProject.projectDir, '.sign/' + rootProject.signProperties['store.file'])
                storePassword rootProject.signProperties['store.password']
                keyAlias rootProject.signProperties['key.alias']
                keyPassword rootProject.signProperties['key.password']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig rootProject.signProperties ? signingConfigs.main : null
        }
        release {
            signingConfig rootProject.signProperties ? signingConfigs.main : null
        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName variant.applicationId
    }
}

dependencies {
    compile project(':core')
    compile 'com.android.support:support-annotations:20.0.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'org.androidannotations:androidannotations-api:3.1'
    compile project(':readiumSdk')

    apt 'org.androidannotations:androidannotations:3.1'
}

ext {
    archivesBaseName = rootProject.name + '-' + rootProject.version + '-b' + rootProject.buildCounter
}

sonarRunner {
    sonarProperties {
        properties['sonar.sources'] = 'src/main/java'
        properties['sonar.binaries'] = 'build/intermediates/classes/debug'
        properties['sonar.profile'] = 'Android Lint'
    }
}
